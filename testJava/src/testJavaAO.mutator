generate exhaustive mutants
in "data/out/"
from "data/model/"
metamodel "/testJava/data/model/java.ecore"
description "Arithmetic operator replacement"

with blocks {
	aorbp "Addition" {
		modify one InfixExpression where {operator in ['-', '*', '/', '%'] and leftOperand <> null and rightOperand <> null}
		with {operator = '+'}
	}
	aorbm "Subtraction" {
		modify one InfixExpression where {operator in ['+', '*', '/', '%'] and leftOperand <> null and rightOperand <> null
			and leftOperand not typed StringLiteral and rightOperand not typed StringLiteral}
		with {operator = '-'}
	}
	aorbt "Times" {
		modify one InfixExpression where {operator in ['+', '-', '/', '%'] and leftOperand <> null and rightOperand <> null
			and leftOperand not typed StringLiteral and rightOperand not typed StringLiteral}
		with {operator = '*'}
	}
	aorbd "Division" {
		modify one InfixExpression where {operator in ['+', '-', '*', '%'] and leftOperand <> null and rightOperand <> null
			and leftOperand not typed StringLiteral and rightOperand not typed StringLiteral}
		with {operator = '/'}
	}
	aorbr "Modulus" {
		modify one InfixExpression where {operator in ['+', '-', '*', '/'] and leftOperand <> null and rightOperand <> null
			and leftOperand not typed StringLiteral and rightOperand not typed StringLiteral}
		with {operator = '%'}
	}
	aorsi "Postfix increment by decrement" {
		modify one PostfixExpression where {operator = '++' and operand <> null}
		with {operator = '--'}
	}
	aorsd "Postfix decrement by increment" {
		modify one PostfixExpression where {operator = '--' and operand <> null}
		with {operator = '++'}
	}
	aorspi "Prefix increment by decrement" {
		modify one PrefixExpression where {operator = '++' and operand <> null}
		with {operator = '--'}
	}
	aorspd "Prefix decrement by increment" {
		modify one PrefixExpression where {operator = '--' and operand <> null}
		with {operator = '++'}
	}
	aoium "Positive to negative" {
		modify one PrefixExpression where {operator = '+' and operand <> null}
		with {operator = '-'} 
	}
	aoiup "Negative to positive" {
		modify one PrefixExpression where {operator = '-' and operand <> null}
		with {operator = '+'}
	}
	aoisd "Positive or negative to prefix decrement" {
		modify one PrefixExpression where {operator in ['+', '-'] and operand <> null and operand not typed NumberLiteral}
		with {operator = '--'}
	}
	aoisi "Positive or negative to prefix increment" {
		modify one PrefixExpression where {operator in ['+', '-'] and operand <> null and operand not typed NumberLiteral}
		with {operator = '++'}
	}
	aods "Prefix increment or decrement deletion" {
		modify one PrefixExpression where {operator in ['++', '--'] and operand <> null}
		with {operator = '+'}
	}
}
