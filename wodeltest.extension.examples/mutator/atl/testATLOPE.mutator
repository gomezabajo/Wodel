generate 2 mutants
in "data/out/"
from "data/model/"
metamodel "/[@**@]/data/model/ATL.ecore"
with resources from
	{input="data/model/in" metamodel="/[@**@]/data/model/MyEcore.ecore"},
	{output="data/model/out" metamodel="/[@**@]/data/model/MyEcore.ecore"}

with blocks {
	cope "Create out pattern" {
		cl = select one EClass from output resources
		p = select one OutPattern
		mod = select one OclModel in p->elements->type->model
		ope = create SimpleOutPatternElement in p->elements
   			with {varName = random-string(2, 4)}
		elem = create OclModelElement in ope->type
   			with {copy(name, cl.name), variableDeclaration = ope}
		modify mod with {elements += elem}
	}
	rope "Remove out pattern" {
		remove one OutPatternElement
	}
	ccope "Class change out pattern" {
		p = select one OutPattern
		type = select one OclModelElement in p->elements->type
		cl = select one EClass from output resources where {name <> type.name}
		modify type with {copy(name, cl.name)}
	}
	ncope "Name change out pattern" {
		modify one OutPatternElement with {varName = random-string(4, 6)}
	}
}