generate 10 mutants
in "data/out/"
from "data/model/"
metamodel "/[@**@]/data/model/DFAAutomaton.ecore"

with blocks {
	cfs "Create final state" {
		s = create State with {isFinal = true}
		create Transition with {tar = s, symbol = one Symbol}
	}
	ccs "Create state" {
		s = create State with {name = random-string(1, 4)}
		t = create Transition with {tar = s, symbol = one Symbol}
	}
	dtr "Delete transition "{
		remove one Transition
	}
	dst "Delete state" {
		remove one State where {isInitial = false}
		remove all Transition where {src = null}
		remove all Transition where {tar = null}
	}
	mst "Change symbol of a transition" {
		modify target symbol from one Transition to other Symbol
	}
	mfs "Set final state to non-final" {
		modify one State where {isFinal = true} with {reverse(isFinal)}
	}
	sst "Swap symbols of two transitions" {
		t = select one Transition
		modify one Transition where {self <> t and src = t->src} with {swapref(symbol, t->symbol)}
	}
	rts "Set transition's target to a new final state" {
		s = create State with {name = 'f', isFinal = true}
		modify target tar from one Transition to s
	}
	clt "Create lambda transition" {
		create Transition
	}
	cst "Change transition's target" {
		t = select one Transition where {symbol <> null}
		create Transition with {src = t->src, symbol = t->symbol, tar = one State where {self <> t->tar}}
	}
}