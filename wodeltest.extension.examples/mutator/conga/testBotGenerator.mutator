generate exhaustive mutants
in "data/out/"
from "data/model/"
metamodel "/testBotGenerator/data/model/BotGenerator.ecore"
with resources from
	{annotation="data/model/Annotation" metamodel="/testBotGenerator/data/model/Annotation.ecore"}

with blocks {
	dp_max "Borrar una frase de entrenamiento (mayor similitud)" {
		tpi = select one IntentValue from annotation resources
		i = select one Intent where {self = tpi->intent}
		li = select one LanguageIntent in i->inputs
		remove one TrainingPhrase from li->inputs where {self = tpi->max1}
	}
	dp_min "Borrar una frase de entrenamiento (menor similitud)" {
		tpi = select one IntentValue from annotation resources
		i = select one Intent where {self = tpi->intent}
		li = select one LanguageIntent in i->inputs
		remove one TrainingPhrase from li->inputs where {self = tpi->min1}
	}
 	dpwp "Borrar todas las frases de un parámetro requerido" {
		p = select one Parameter where {required = true}
		remove all TrainingPhrase where {tokens->parameter = p}
		remove all LanguageIntent where {inputs = null}
	}
	dpwl "Borrar todas las frases que utilicen un literal en concreto" {
		input = select one SimpleInput
		prt = select one ParameterReferenceToken where {textReference = input.values}
		remove all TrainingPhrase where {tokens = prt}
	}
	k2p_max "Eliminar frases de entrenamiento de Intents hasta trainingPhrases->size() < 3 (mayor similitud)" {
		tpi = select one IntentValue from annotation resources
		i = select one Intent where {self = tpi->intent}
		li = select one LanguageIntent in i->inputs
		tp0 = select one TrainingPhrase in li->inputs where {self = tpi->max1}
		tp1 = select one TrainingPhrase in li->inputs where {self = tpi->max2 and self <> tp0}
		tp2 = select one TrainingPhrase in li->inputs where {self <> tp0 and self <> tp1}
		remove all TrainingPhrase from li->inputs where {self <> tp0 and self <> tp1}
	}
	k2p_min "Eliminar frases de entrenamiento de Intents hasta trainingPhrases->size() < 3 (menor similitud)" {
		tpi = select one IntentValue from annotation resources
		i = select one Intent where {self = tpi->intent}
		li = select one LanguageIntent in i->inputs
		tp0 = select one TrainingPhrase in li->inputs where {self = tpi->min1}
		tp1 = select one TrainingPhrase in li->inputs where {self = tpi->min2 and self <> tp0}
		tp2 = select one TrainingPhrase in li->inputs where {self <> tp0 and self <> tp1}
		remove all TrainingPhrase from li->inputs where {self <> tp0 and self <> tp1}
	}
	mp_max "Mover frases de entrenamiento entre intents al principio de la conversación (mayor similitud)" {
		i1 = select one Intent where {inputs <> null}
		iiv = select one IntentIntentValue from annotation resources where {intent1 = i1 and max1Value = max(max1Value)}
		i2 = select one Intent where {inputs <> null and self = iiv->intent2}
		li1 = select one LanguageIntent in i1->inputs where {inputs <> null}
		li2 = select one LanguageIntent in i2->inputs where {self <> li1 and inputs <> null}
		tp1 = select one TrainingPhrase in li1->inputs where {self = iiv->max1}
		tp2 = select one TrainingPhrase in li1->inputs where {self <> tp1}
		modify li2 with {inputs += tp1}
	}
	mp_min "Mover frases de entrenamiento entre intents al principio de la conversación (menor similitud)" {
		i1 = select one Intent where {inputs <> null}
		iiv = select one IntentIntentValue from annotation resources where {intent1 = i1 and min1Value = min(min1Value)}
		i2 = select one Intent where {inputs <> null and self = iiv->intent2}
		li1 = select one LanguageIntent in i1->inputs where {inputs <> null}
		li2 = select one LanguageIntent in i2->inputs where {self <> li1 and inputs <> null}
		tp1 = select one TrainingPhrase in li1->inputs where {self = iiv->min1}
		tp2 = select one TrainingPhrase in li1->inputs where {self <> tp1}
		modify li2 with {inputs += tp1}
	}
	rip "Eliminar todas las frases de un parametro y ese parametro" {
		p = select one Parameter
		remove all TrainingPhrase where {tokens->parameter = p}
		remove all ParameterToken where {parameter = p}
		remove p
		remove all LanguageIntent where {inputs = null}
	}
	dpp "Borrar el prompt de un parámetro que sea requerido" {
		p0 = select one Parameter where {required = true}
		remove one LanguagePrompt from p0->prompts
	}
	spo "Cambiar un parametro requerido a opcional" {
		modify one Parameter where {required = true} with {required = false}
	}
	cre "Cambiar expresion regular" {
		modify one RegexInput with {expresion = catstart('[^'), expresion = catend(']')}
	}
	dle "Elimina un literal de una entity" {
		remove one SimpleInput 
	}
	dfi "Borrar un intent que tenga fallback=true" {
		i = select one Intent where {fallbackIntent = true}
		remove all UserInteraction where {intent = i}
		remove i
	}
	da "Borrar acciones asociadas a un BotInteraction" {
		bi0 = select one BotInteraction where {actions <> null}
		remove one Action from bi0->actions 
	}
	dpr "Borrar un parametro que se utilice en una respuesta" {
		text = select one TextInput where {tokens is typed ParameterToken}
		remove one ParameterToken from text->tokens
	}
	so "Swap outputs between two consecutive steps" {
		bi1 = select one BotInteraction where {actions <> null and outcoming->^target <> null}
		ui1 = select one UserInteraction in bi1->outcoming where {^target <> null}
		bi2 = select one BotInteraction in ui1->^target where {self <> bi1 and actions <> null and actions <> bi1->actions}
		modify bi1 with {swapref(actions, bi2->actions)}
	}
	dcs "Saltarse un paso en una interacción" {
		ui0 = select one UserInteraction where {^target->outcoming->^target is typed BotInteraction}
		bi0 = select one BotInteraction in ui0->^target->outcoming->^target
		modify target ^target from ui0 to bi0
	}
	dcb "Eliminar bifurcación en un flujo de conversación" {
		bi = select one BotInteraction where {outcoming <> null}
		ui = select one UserInteraction in bi->outcoming
		remove one UserInteraction from bi->outcoming where {self <> ui}
	}
}
/*
	so_max "Swap outputs by intent similarity (max) " {
		ui1 = select one UserInteraction where {^target <> null}
		i1 = select one Intent in ui1->intent where {inputs <> null}
		iiv = select one IntentIntentValue from annotation resources where {intent1 = i1 and max1Value = max(max1Value)} 
		ui2 = select one UserInteraction where {self <> ui1}
		i2 = select one Intent in ui2->intent where {inputs <> null and self = iiv->intent2}
		bi1 = select one BotInteraction in ui1->^target where {actions <> null}
		bi2 = select one BotInteraction in ui2->^target where {self <> bi1 and actions <> null and actions <> bi1->actions}
		modify bi1 with {swapref(actions, bi2->actions)}
	}
	so_min "Swap outputs by intent similarity (min) " {
		ui1 = select one UserInteraction where {^target <> null}
		i1 = select one Intent in ui1->intent where {inputs <> null}
		iiv = select one IntentIntentValue from annotation resources where {intent1 = i1 and min1Value = min(min1Value)} 
		ui2 = select one UserInteraction where {self <> ui1}
		i2 = select one Intent in ui2->intent where {inputs <> null and self = iiv->intent2}
		bi1 = select one BotInteraction in ui1->^target where {actions <> null}
		bi2 = select one BotInteraction in ui2->^target where {self <> bi1 and actions <> null and actions <> bi1->actions}
		modify bi1 with {swapref(actions, bi2->actions)}
	}
*/
