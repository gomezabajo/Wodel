generate 2 mutants
in "data/out/"
from "data/model/"
metamodel "/WodelTest4LC/data/model/LogicCircuit.ecore"

with blocks {
	mtg "Retype a two-pinned gate as a two-pinned gate of a different type" {
		retype one [AND, OR] as [AND, OR]
	}
	rnot "Removes a NOT gate" {
		n0 = select one NOT where {input->src <> null and output->tar <> null}
		i0 = select one InputPin in n0->output->tar
		o0 = select one OutputPin in n0->input->src
		modify i0 with {src = o0}
		remove n0
	}
	anot "Adds a NOT gate" {
		g0 = select one Gate where {input <> null and output <> null}
		n1 = create NOT with {name = random-string(2, 4)}
		o0 = select one OutputPin in g0->output
		i0 = select one InputPin in o0->tar
		create OutputPin in n1->output 
    		with {name = random-string(2, 4), tar = i0}
		create InputPin in n1->input 
    		with {name = random-string(2, 4), src = o0}
    }
}
